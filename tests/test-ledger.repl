(env-data {"disable-marmalade":true})
(load "kadena_repl_sandbox/kda-env/init.repl")
(load "init/init-ledger.repl")

(begin-tx)
(load "tools/events_analyzer.pact")
(commit-tx)


(begin-tx)
(namespace 'marmalade-ng)
(load "stubs/policy-events-A.pact")
(load "stubs/policy-events-B.pact")
(commit-tx)

(begin-tx "Create a first token")
(use marmalade-ng.ledger)
(env-data {'create-ks:["create"]})

(print (+ "Token-1: => " (create-token-id (read-keyset 'create-ks) "http://token-1" )))

; try to create without signature
(env-sigs [])
(expect-failure "Creation guard not signed" "Keyset failure"
  (create-token "t:r-4jQUrZWpYfEqq_iGvBn1ofgCLPoh2ZfG5kfAVX2KM" 1 "http://token-1"
                [marmalade-ng.policy-events-A, marmalade-ng.policy-events-B] (read-keyset 'create-ks)))

; try to create with an error in token-id
(env-sigs [{'key:"create", 'caps:[(ENFORCE-RESERVED)]}])
(expect-failure "Bad token ID" "Token protocol violation"
  (create-token "t:bad" 1 "http://token-1"
                [marmalade-ng.policy-events-A, marmalade-ng.policy-events-B] (read-keyset 'create-ks)))

; Now try with evrything good
(env-events true)
(create-token "t:r-4jQUrZWpYfEqq_iGvBn1ofgCLPoh2ZfG5kfAVX2KM" 1 "http://token-1"
              [marmalade-ng.policy-events-A, marmalade-ng.policy-events-B] (read-keyset 'create-ks))

; Check the events
(ev-analyzer.store (env-events true))
(expect "Event TOKEN-CREATE has been emitted" true (ev-analyzer.is-present "TOKEN-CREATE"))
(expect "Event TOKEN-CREATE -> token_id" "t:r-4jQUrZWpYfEqq_iGvBn1ofgCLPoh2ZfG5kfAVX2KM" (ev-analyzer.param "TOKEN-CREATE" 0))
(expect "Event TOKEN-CREATE -> token_id" "http://token-1" (ev-analyzer.param "TOKEN-CREATE" 1))
(expect "Event TOKEN-CREATE -> prec" 1 (ev-analyzer.param "TOKEN-CREATE" 2))
(expect-that "Event TOKEN-CREATE -> policies" (contains "marmalade-ng.policy-events-A") (ev-analyzer.param "TOKEN-CREATE" 3))
(expect-that "Event TOKEN-CREATE -> policies" (contains "marmalade-ng.policy-events-B") (ev-analyzer.param "TOKEN-CREATE" 3))


(expect "Policy A has been called" true (ev-analyzer.is-present "EVENT-A"))
(expect "Policy B has been called" true (ev-analyzer.is-present "EVENT-B"))

(expect "Policy A params" "init" (ev-analyzer.param "EVENT-A" 0))
(expect "Policy B params" "init" (ev-analyzer.param "EVENT-B" 0))

(expect "Policy B called beofre A " true (< (ev-analyzer.position "EVENT-B")
                                            (ev-analyzer.position "EVENT-A")))

; Check URI
(expect "URI is good" "http://token-1" (get-uri "t:r-4jQUrZWpYfEqq_iGvBn1ofgCLPoh2ZfG5kfAVX2KM"))
; Check policies
(expect "Policies are good and ordered by rank" [marmalade-ng.policy-events-B, marmalade-ng.policy-events-A] (get-policies "t:r-4jQUrZWpYfEqq_iGvBn1ofgCLPoh2ZfG5kfAVX2KM"))
; Check precision
(expect "Precision" 1 (precision "t:r-4jQUrZWpYfEqq_iGvBn1ofgCLPoh2ZfG5kfAVX2KM"))
; Check total supply
(expect "Total supply empty" 0.0 (total-supply "t:r-4jQUrZWpYfEqq_iGvBn1ofgCLPoh2ZfG5kfAVX2KM"))
; Check token-exist
(expect "Token exist" true (token-exist "t:r-4jQUrZWpYfEqq_iGvBn1ofgCLPoh2ZfG5kfAVX2KM"))
; Check a not existent token
(expect "Token exist" false (token-exist "t:doesnotexist"))
(commit-tx)



;-------------------------------------------
; Minting tests
;--------------------------------------------
; We want to mint 60% of the NFT to alice
;             and 40% to bob
(begin-tx "Mint token")
(env-data {'alice-ks:["alice-key"], 'bob-ks:["bob-key"] })
(use marmalade-ng.ledger)
(use free.util-lists)

(env-events true)
(mint "t:r-4jQUrZWpYfEqq_iGvBn1ofgCLPoh2ZfG5kfAVX2KM" "alice" (read-keyset 'alice-ks) 0.6)

; Check the events
(ev-analyzer.store (env-events true))

(expect "Event MINT has been emitted" true (ev-analyzer.is-present "MINT"))
(expect "Event MINT -> token-id" "t:r-4jQUrZWpYfEqq_iGvBn1ofgCLPoh2ZfG5kfAVX2KM" (ev-analyzer.param "MINT" 0))
(expect "Event MINT -> account" "alice" (ev-analyzer.param "MINT" 1))
(expect "Event MINT -> amount" 0.6 (ev-analyzer.param "MINT" 2))

(expect "Event SUPPLY has been emitted" true (ev-analyzer.is-present "SUPPLY"))
(expect "Event SUPPLY -> token-id" "t:r-4jQUrZWpYfEqq_iGvBn1ofgCLPoh2ZfG5kfAVX2KM" (ev-analyzer.param "SUPPLY" 0))
(expect "Event SUPPLY -> amount" 0.6 (ev-analyzer.param "SUPPLY" 1))

(expect "Event RECONCILE has been emitted" true (ev-analyzer.is-present "RECONCILE"))
(expect "Event RECONCILE -> token-id" "t:r-4jQUrZWpYfEqq_iGvBn1ofgCLPoh2ZfG5kfAVX2KM" (ev-analyzer.param "RECONCILE" 0))
(expect "Event RECONCILE -> amount" {"account": "alice","current": 0.6,"previous": 0.0}
                                    (ev-analyzer.param "RECONCILE" 3))

(expect "Policy A has been called" true (ev-analyzer.is-present "EVENT-A"))
(expect "Policy B has been called" true (ev-analyzer.is-present "EVENT-B"))

(expect "Policy A params" "mint" (ev-analyzer.param "EVENT-A" 0))
(expect "Policy B params" "mint" (ev-analyzer.param "EVENT-B" 0))

(mint "t:r-4jQUrZWpYfEqq_iGvBn1ofgCLPoh2ZfG5kfAVX2KM" "bob" (read-keyset 'bob-ks) 0.4)

; Check the balances
(expect "Balance of alice is 0.6" 0.6 (get-balance "t:r-4jQUrZWpYfEqq_iGvBn1ofgCLPoh2ZfG5kfAVX2KM" "alice"))
(expect "Balance of bob is 0.4" 0.6 (get-balance "t:r-4jQUrZWpYfEqq_iGvBn1ofgCLPoh2ZfG5kfAVX2KM" "alice"))

; Check the guards
(expect "Alice has the good guard" (read-keyset 'alice-ks) (account-guard "t:r-4jQUrZWpYfEqq_iGvBn1ofgCLPoh2ZfG5kfAVX2KM" "alice"))
(expect "Bob has the good guard" (read-keyset 'bob-ks) (account-guard "t:r-4jQUrZWpYfEqq_iGvBn1ofgCLPoh2ZfG5kfAVX2KM" "bob"))

; List the balances
(expect-that "Balance list has 1 element" (compose (length) (= 1)) (list-balances "alice"))
(expect-that "Balance element is right" (compose (first) (= {"balance": 0.6,"id": "t:r-4jQUrZWpYfEqq_iGvBn1ofgCLPoh2ZfG5kfAVX2KM"})) (list-balances "alice"))
(commit-tx)

;-------------------------------------------------------------------------------
; Burning Test
;-------------------------------------------------------------------------------
; We will create and mint a new token for burning tests
(begin-tx "Burn tests")
(use marmalade-ng.ledger)
(env-data {'create-ks:["create"],
          'alice-ks:["alice-key"]})

(env-sigs [{'key:"create", 'caps:[(ENFORCE-RESERVED)]}])

(print (+ "Token-2: => " (create-token-id (read-keyset 'create-ks) "http://token-2" )))
(create-token "t:QvuWzPPKhSlueC9hryQKJ-ItFPGtdOhDDhrD4q8lc-I" 1 "http://token-2"
              [marmalade-ng.policy-events-A, marmalade-ng.policy-events-B] (read-keyset 'create-ks))

(mint "t:QvuWzPPKhSlueC9hryQKJ-ItFPGtdOhDDhrD4q8lc-I" "alice" (read-keyset 'alice-ks) 1.0)

; Sanity check => Verify the balance of alice
(expect "Balance of alice should be 1.0" 1.0 (get-balance "t:QvuWzPPKhSlueC9hryQKJ-ItFPGtdOhDDhrD4q8lc-I" "alice"))
; and the total supply
(expect "Total supply must be 1.0" 1.0 (total-supply "t:QvuWzPPKhSlueC9hryQKJ-ItFPGtdOhDDhrD4q8lc-I"))


; Try to burn without signature
(env-sigs [])
(expect-failure "Signature/and cap needed" "Managed capability not installed"
                (burn "t:QvuWzPPKhSlueC9hryQKJ-ItFPGtdOhDDhrD4q8lc-I" "alice" 0.1))

; Signature but bad cap
(env-sigs [{'key:"alice-key", 'caps:[(BURN "t:QvuWzPPKhSlueC9hryQKJ-ItFPGtdOhDDhrD4q8lc-I" "bob" 0.1)]}])

(expect-failure "Signature/and cap needed" "Managed capability not installed"
                (burn "t:QvuWzPPKhSlueC9hryQKJ-ItFPGtdOhDDhrD4q8lc-I" "alice" 0.1))

; Good cap but bad signer
(env-sigs [{'key:"nobody", 'caps:[(BURN "t:QvuWzPPKhSlueC9hryQKJ-ItFPGtdOhDDhrD4q8lc-I" "alice" 0.1)]}])
(expect-failure "Signature/and cap needed" "Keyset failure"
                (burn "t:QvuWzPPKhSlueC9hryQKJ-ItFPGtdOhDDhrD4q8lc-I" "alice" 0.1))

; Evrything good
(env-sigs [{'key:"alice-key", 'caps:[(BURN "t:QvuWzPPKhSlueC9hryQKJ-ItFPGtdOhDDhrD4q8lc-I" "alice" 0.1)]}])

(env-events true)
(burn "t:QvuWzPPKhSlueC9hryQKJ-ItFPGtdOhDDhrD4q8lc-I" "alice" 0.1)

; Check the events
(ev-analyzer.store (env-events true))
(expect "Event BURN has been emitted" true (ev-analyzer.is-present "BURN"))
(expect "Event BURN -> token-id" "t:QvuWzPPKhSlueC9hryQKJ-ItFPGtdOhDDhrD4q8lc-I" (ev-analyzer.param "BURN" 0))
(expect "Event BURN -> account" "alice" (ev-analyzer.param "BURN" 1))
(expect "Event BURN -> amount" 0.1 (ev-analyzer.param "BURN" 2))

(expect "Event SUPPLY has been emitted" true (ev-analyzer.is-present "SUPPLY"))
(expect "Event SUPPLY -> amount" 0.9 (ev-analyzer.param "SUPPLY" 1))

(expect "Event RECONCILE has been emitted" true (ev-analyzer.is-present "RECONCILE"))
(expect "Event RECONCILE -> token-id" "t:QvuWzPPKhSlueC9hryQKJ-ItFPGtdOhDDhrD4q8lc-I" (ev-analyzer.param "RECONCILE" 0))
(expect "Event RECONCILE -> amount" {"account": "alice","current": 0.9,"previous": 1.0}
                                    (ev-analyzer.param "RECONCILE" 2))


(expect "Policy A has been called" true (ev-analyzer.is-present "EVENT-A"))
(expect "Policy B has been called" true (ev-analyzer.is-present "EVENT-B"))

(expect "Policy A params" "burn" (ev-analyzer.param "EVENT-A" 0))
(expect "Policy B params" "burn" (ev-analyzer.param "EVENT-B" 0))

; Check the balance
(expect "Balance of alice should be 0.9" 0.9 (get-balance "t:QvuWzPPKhSlueC9hryQKJ-ItFPGtdOhDDhrD4q8lc-I" "alice"))

; Check the supply
(expect "Total supply must be 0.9 (1.0 - 0.1 burnt)" 0.9 (total-supply "t:QvuWzPPKhSlueC9hryQKJ-ItFPGtdOhDDhrD4q8lc-I"))
(commit-tx)

;-------------------------------------------------------------------------------
; Transfer test
;-------------------------------------------------------------------------------
; We will create and mint a new token for burning tests
(begin-tx "Transfer tests")
(use marmalade-ng.ledger)
(env-data {'create-ks:["create"],
          'alice-ks:["alice-key"]})


(print (+ "Token-3: => " (create-token-id (read-keyset 'create-ks) "http://token-3-test-transfer" )))

(env-sigs [{'key:"create", 'caps:[(ENFORCE-RESERVED)]}])

(create-token "t:8XZTuI-LC_Zww0mfCzFa8l7dkbZirdM22KJwRQ5PlRg" 1 "http://token-3-test-transfer"
              [marmalade-ng.policy-events-A, marmalade-ng.policy-events-B] (read-keyset 'create-ks))

(mint "t:8XZTuI-LC_Zww0mfCzFa8l7dkbZirdM22KJwRQ5PlRg" "alice" (read-keyset 'alice-ks) 1.0)

; Sanity check => Verify the balance of alice
(expect "Balance of alice should be 1.0" 1.0 (get-balance "t:8XZTuI-LC_Zww0mfCzFa8l7dkbZirdM22KJwRQ5PlRg" "alice"))

; Transfer without the cap
(env-data {'bob-ks:["bob-key"]})
(expect-failure "Signature/and cap needed" "Managed capability not installed"
                (transfer-create "t:8XZTuI-LC_Zww0mfCzFa8l7dkbZirdM22KJwRQ5PlRg" "alice" "bob" (read-keyset 'bob-ks) 0.1))

; Good cap but bad signer
(env-sigs [{'key:"nobody", 'caps:[(TRANSFER "t:8XZTuI-LC_Zww0mfCzFa8l7dkbZirdM22KJwRQ5PlRg" "alice" "bob" 0.1)]}
          ])
(expect-failure "Signature/and cap needed" "Keyset failure"
  (transfer-create "t:8XZTuI-LC_Zww0mfCzFa8l7dkbZirdM22KJwRQ5PlRg" "alice" "bob" (read-keyset 'bob-ks) 0.1))


(env-sigs [{'key:"alice-key", 'caps:[(TRANSFER "t:8XZTuI-LC_Zww0mfCzFa8l7dkbZirdM22KJwRQ5PlRg" "alice" "bob" 0.2)]}
          ])

(env-events true)
(transfer-create "t:8XZTuI-LC_Zww0mfCzFa8l7dkbZirdM22KJwRQ5PlRg" "alice" "bob" (read-keyset 'bob-ks) 0.1)
(ev-analyzer.store (env-events true))

(expect "Event TRANSFER has been emitted" true (ev-analyzer.is-present "TRANSFER"))

(expect "Event SUPPLY has not been emitted" false (ev-analyzer.is-present "SUPPLY"))

(expect "Event RECONCILE has been emitted" true (ev-analyzer.is-present "RECONCILE"))
(expect "Event RECONCILE -> token-id" "t:8XZTuI-LC_Zww0mfCzFa8l7dkbZirdM22KJwRQ5PlRg" (ev-analyzer.param "RECONCILE" 0))
(expect "Event RECONCILE -> amount" {"account": "alice","current": 0.9,"previous": 1.0}
                                    (ev-analyzer.param "RECONCILE" 2))
(expect "Event RECONCILE -> amount" {"account": "bob","current": 0.1,"previous": 0.0}
                                    (ev-analyzer.param "RECONCILE" 3))

(expect "Policy A has been called" true (ev-analyzer.is-present "EVENT-A"))
(expect "Policy B has been called" true (ev-analyzer.is-present "EVENT-B"))

(expect "Policy A params" "transfer" (ev-analyzer.param "EVENT-A" 0))
(expect "Policy B params" "transfer" (ev-analyzer.param "EVENT-B" 0))

(expect "Balance of alice should be 0.9" 0.9 (get-balance "t:8XZTuI-LC_Zww0mfCzFa8l7dkbZirdM22KJwRQ5PlRg" "alice"))
(expect "Balance of bob should be 0.1" 0.1 (get-balance "t:8XZTuI-LC_Zww0mfCzFa8l7dkbZirdM22KJwRQ5PlRg" "bob"))

; Since bob has now an account, we can try a simple transfer (not transfer-create)
(transfer "t:8XZTuI-LC_Zww0mfCzFa8l7dkbZirdM22KJwRQ5PlRg" "alice" "bob"  0.1)

(expect "Balance of alice should be 0.8" 0.8 (get-balance "t:8XZTuI-LC_Zww0mfCzFa8l7dkbZirdM22KJwRQ5PlRg" "alice"))
(expect "Balance of bob should be 0.2" 0.2 (get-balance "t:8XZTuI-LC_Zww0mfCzFa8l7dkbZirdM22KJwRQ5PlRg" "bob"))

(commit-tx)

;-------------------------------------------------------------------------------
; Sales tests
;-------------------------------------------------------------------------------
(begin-tx "Sales")
(env-hash (hash "sale 1"))
(print (format "Sale ID 1: => {}" [(tx-hash)]))


; Alice will sell 0.1 to carol
(use marmalade-ng.ledger)

(env-sigs [ {'key:"alice-key", 'caps:[(OFFER "t:r-4jQUrZWpYfEqq_iGvBn1ofgCLPoh2ZfG5kfAVX2KM" "alice" 0.1)]}
           ])
(env-chain-data {"block-time": (time "2023-01-01T00:00:00Z")})

(expect "Balance of was 0.6" 0.6 (get-balance "t:r-4jQUrZWpYfEqq_iGvBn1ofgCLPoh2ZfG5kfAVX2KM" "alice"))

;Try to sell with a bad timeout
(expect-failure "Tiemout in the past" "Invalid timeout"
                (sale "t:r-4jQUrZWpYfEqq_iGvBn1ofgCLPoh2ZfG5kfAVX2KM" "alice" 0.1 (time "2019-06-01T00:00:00Z")))
(expect-failure "Tiemout in 11 years" "Invalid timeout"
                (sale "t:r-4jQUrZWpYfEqq_iGvBn1ofgCLPoh2ZfG5kfAVX2KM" "alice" 0.1 (time "2043-07-01T00:00:00Z")))


(sale "t:r-4jQUrZWpYfEqq_iGvBn1ofgCLPoh2ZfG5kfAVX2KM" "alice" 0.1 (time "2023-06-01T00:00:00Z"))

; Check the events
(ev-analyzer.store (env-events true))

(expect "Event OFFER has been emitted" true (ev-analyzer.is-present "OFFER"))
(expect "Event SALE has been emitted" true (ev-analyzer.is-present "SALE"))
(expect "Event SUPPLY has not been emitted" false (ev-analyzer.is-present "SUPPLY"))

(expect "Event SALE -> token_id" "t:r-4jQUrZWpYfEqq_iGvBn1ofgCLPoh2ZfG5kfAVX2KM" (ev-analyzer.param "SALE" 0))
(expect "Event SALE -> seller" "alice" (ev-analyzer.param "SALE" 1))
(expect "Event SALE -> amount" 0.1 (ev-analyzer.param "SALE" 2))

(expect "Event RECONCILE has been emitted" true (ev-analyzer.is-present "RECONCILE"))
(expect "Event RECONCILE -> token-id" "t:r-4jQUrZWpYfEqq_iGvBn1ofgCLPoh2ZfG5kfAVX2KM" (ev-analyzer.param "RECONCILE" 0))
(expect "Event RECONCILE -> amount" {"account": "alice","current": 0.5,"previous": 0.6}
                                    (ev-analyzer.param "RECONCILE" 2))


(expect "Policy A has been called" true (ev-analyzer.is-present "EVENT-A"))
(expect "Policy B has been called" true (ev-analyzer.is-present "EVENT-B"))

(expect "Policy A params" "sale-offer" (ev-analyzer.param "EVENT-A" 0))
(expect "Policy B params" "sale-offer" (ev-analyzer.param "EVENT-B" 0))

(expect "Balance of alice is now 0.5" 0.5 (get-balance "t:r-4jQUrZWpYfEqq_iGvBn1ofgCLPoh2ZfG5kfAVX2KM" "alice"))

(commit-tx)


(begin-tx "Withdraw Sale")
(env-hash (hash "withdraw 1"))
(use marmalade-ng.ledger)
; Do a rollback
(continue-pact 0 true (hash "sale 1"))

; Check the events
(ev-analyzer.store (env-events true))

(expect "Event SUPPLY has not been emitted" false (ev-analyzer.is-present "SUPPLY"))

(expect "Event RECONCILE has been emitted" true (ev-analyzer.is-present "RECONCILE"))
(expect "Event RECONCILE -> token-id" "t:r-4jQUrZWpYfEqq_iGvBn1ofgCLPoh2ZfG5kfAVX2KM" (ev-analyzer.param "RECONCILE" 0))
(expect "Event RECONCILE -> amount" {"account": "alice","current": 0.6,"previous": 0.5}
                                    (ev-analyzer.param "RECONCILE" 3))


(expect "Policy A has been called" true (ev-analyzer.is-present "EVENT-A"))
(expect "Policy B has been called" true (ev-analyzer.is-present "EVENT-B"))

(expect "Policy A params" "sale-withdraw" (ev-analyzer.param "EVENT-A" 0))
(expect "Policy B params" "sale-withdraw" (ev-analyzer.param "EVENT-B" 0))

(expect "Balance of alice is now back 0.6" 0.6 (get-balance "t:r-4jQUrZWpYfEqq_iGvBn1ofgCLPoh2ZfG5kfAVX2KM" "alice"))
(rollback-tx)


(begin-tx "Complete Sale")
(env-hash (hash "complete 1"))
(use marmalade-ng.ledger)
; Do a rollback
(env-data {'buyer: "carol", 'buyer-guard: {'pred:"keys-all", 'keys:["carol-key"]}})

(continue-pact 1 false (hash "sale 1"))

; Check the events
(ev-analyzer.store (env-events true))

(expect "Event RECONCILE has been emitted" true (ev-analyzer.is-present "RECONCILE"))
(expect "Event RECONCILE -> token-id" "t:r-4jQUrZWpYfEqq_iGvBn1ofgCLPoh2ZfG5kfAVX2KM" (ev-analyzer.param "RECONCILE" 0))
(expect "Event RECONCILE -> amount" {"account": "carol","current": 0.1,"previous": 0.0}
                                    (ev-analyzer.param "RECONCILE" 3))

(expect "Policy A has been called" true (ev-analyzer.is-present "EVENT-A"))
(expect "Policy B has been called" true (ev-analyzer.is-present "EVENT-B"))

(expect "Policy A params" "sale-buy" (ev-analyzer.param "EVENT-A" 0))
(expect "Policy B params" "sale-buy" (ev-analyzer.param "EVENT-B" 0))

(expect "Balance of alice is still 0.5" 0.5 (get-balance "t:r-4jQUrZWpYfEqq_iGvBn1ofgCLPoh2ZfG5kfAVX2KM" "alice"))

; But carol got the NFT
(expect "Balance of carol is now 0.1" 0.1 (get-balance "t:r-4jQUrZWpYfEqq_iGvBn1ofgCLPoh2ZfG5kfAVX2KM" "carol"))

(commit-tx)



;;; -------- Do addtional checks to prevent nested pact
; We create a wrapper contract containing a "parent" pact and verify that it's
; not able to call and nest the official ledger pact.
(begin-tx)
(namespace 'free)
(module nested-pacts-mod G
  (defcap G() true)

  (defpact sale (id:string seller:string amount:decimal timeout:time)
    (step-with-rollback
      (marmalade-ng.ledger.sale id seller amount timeout)
      (continue (marmalade-ng.ledger.sale id seller amount timeout)))
    (step
      (continue (marmalade-ng.ledger.sale id seller amount timeout)))
  )
)
(commit-tx)

(begin-tx)
(expect-failure "No Nested Pacts" "Nested pact not allowed"
  (free.nested-pacts-mod.sale "t:r-4jQUrZWpYfEqq_iGvBn1ofgCLPoh2ZfG5kfAVX2KM" "alice" 0.1 (time "2023-06-01T00:00:00Z")))
(commit-tx)
